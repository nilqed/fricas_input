)abbrev category PUNIT PhysicalUnit
PhysicalUnit() : Category == with   
    "*"  : (%,%) -> %
    "/"  : (%,%) -> %
    "^"  : (%,Fraction Integer) -> %
    "="  : (%,%) -> Boolean
    dim  : % -> OutputForm
    one  : %
    coerce : % -> OutputForm


)abbrev domain SI SIunit 
SIunit : Exports == Implementation where

    X  ==> Expression Integer
    Q  ==> Fraction Integer
    
    VI  ==> Vector Q
    LS  ==> List Symbol
    LEI ==> List X
    LVI ==> List VI

    
    Exports == Join(PhysicalUnit,CoercibleTo OutputForm) with

       ----------------------------
        -- (de)-construct an SI unit  
        ----------------------------
        mksi: List(Q) -> %
        si2l: % -> List(Q)

        -----------------------------------------------------------------
        -- constructor for each base unit: e.g. %m(2)=m^2 -> square meter
        -----------------------------------------------------------------
        %m   :  Q -> %
        %kg  :  Q -> %  
        %s   :  Q -> %
        %A   :  Q -> %
        %K   :  Q -> %  
        %mol :  Q -> % 
        %cd  :  Q -> %        
        
        -------------------
        -- SI derived units
        -------------------
        SIderived : String -> %
        
        
        ----------------
        -- Buckingham Pi 
        ----------------
        buck : Table(Symbol,%) -> List(X)
           


    Implementation == add
        
        ---------------------------------------      
        -- Represent as 1-Array [0,0,0,0,0,0,0]
        ---------------------------------------    
        Rep := OneDimensionalArray(Q)

        ---------------------------------
        -- Implementation of constructors
        ---------------------------------
        
        mksi(l) == 
          r:Rep:=new(7,0$Q)$Rep       
          for i in 1..7 repeat r.i := l(i)
          r
          
        si2l(r) ==
          l:List(Q):=[0,0,0,0,0,0,0]
          for i in 1..7 repeat l(i) := r.i
          l

        x * y ==
          z:Rep:=new(7,0$Q)$Rep
          for i in 1..7 repeat z.i := x.i + y.i
          z
          
        x / y ==
          z:Rep:=new(7,0$Q)$Rep
          for i in 1..7 repeat z.i := x.i - y.i
          z
                    
        x ^ n ==
          z:Rep:=new(7,0$Q)$Rep
          for i in 1..7 repeat z.i := x.i * n
          z
         
        x = y ==
          res:Boolean:=true 
          for i in 1..7 repeat res:=(res and test(x.i=y.i)$Boolean) 
          res
       
        ------------------------ 
        -- Coerce to output form
        ------------------------  
        coerce(r : %) : OutputForm ==
          SYM ==> Symbol
          OF  ==> OutputForm
          sym:List(String):=["m","kg","s","A","K","mol","cd"]
          f:OutputForm:=empty()     
          for i in 1..7 repeat 
            if not zero? r.i then
              f:=hconcat(f,sym(i)::SYM::OF ^ r.i::OF)
              --f:=hconcat(f,super(sym(i)::SYM::OF, r.i::OF))
          f
        

        
        ------------------------------------------
        -- Base unit constructors (implementation)
        ------------------------------------------ 
                    
        %m(n:Q) ==
          mksi([n,0,0,0,0,0,0]) 

        %kg(n:Q) ==
          mksi([0,n,0,0,0,0,0]) 
          
        %s(n:Q) ==
          mksi([0,0,n,0,0,0,0])          

        %A(n:Q) ==
          mksi([0,0,0,n,0,0,0]) 

        %K(n:Q) ==
          mksi([0,0,0,0,n,0,0]) 
          
        %mol(n:Q) ==
          mksi([0,0,0,0,0,n,0])
          
        %cd(n:Q) ==
          mksi([0,0,0,0,0,0,n])                   
       
        one() ==
          mksi([0,0,0,0,0,0,0])  
          
          

        
        -- Helper function
        mkxpr(s:LS,v:VI):X == 
          r:X:=1
          for j in 1..#s repeat r:=r*(s.j)::X^(v.j)
          r
          

        -- Buckingham Pi
        buck(t:Table(Symbol,%)):List X ==
          M:Matrix(Q):=matrix [si2l(t.x) for x in keys(t)]
          ns:LVI:=nullSpace transpose(M)
          r:List X:=[]
          for j in 1..#ns repeat r:=append(r,[mkxpr(keys(t),ns.j)])
          r



-- End of SIunit --        
