-------------------------
-- Lorentz transformation
-------------------------
-- Fixed system (x,t), moving (x',t') with speed v
-- Fixed observer sees plot 1, moving observer sees plot2
)clear all
)spool lorentz.out
)sys gnuplot -e "set term dumb; plot [t=0:2] t/2,t"
)sys gnuplot -e "set term dumb; plot [t=0:2] -t/2,t"
--
-- Types
X==>EXPR INT
--tv:X; tc:X, to:X, %tv:X, %tc:X, %to:X, v:X, c:X
--a:X,b:X,r:X,s:X
-- Fixed observer X
Xv := vector [v*tv,tv] 
Xc := vector [c*tc,tc]
Xo := vector [0,to]

-- Moving observer Y
Yv := vector [0,%tv]
Yc := vector [c*%tc,%tc]
Yo := vector [-v*%to,%to]


-- Linear transformation
-- x' = a*x + b*t
-- t' = r*x + s*t
--
L := matrix [[a,b],[r,s]]

-- Equations
eq11 := (L * Xv).1 = Yv.1
eq12 := (L * Xv).2 = Yv.2
eq21 := (L * Xc).1 = Yc.1
eq22 := (L * Xc).2 = Yc.2
eq31 := (L * Xo).1 = Yo.1
eq32 := (L * Xo).2 = Yo.2


r_a := solve(eq11,a).1
r_s := solve(lhs(-v*eq32)=lhs(eq31),s).1
r:=rhs solve(lhs(c*eq22)=lhs(eq21),r).1

r:=subst(r,[r_a,r_s])
a:=rhs r_a
s:=rhs r_s

L := matrix [[a,b],[r,s]]

r_b:=solve(determinant(L)=1,b).2

L := matrix [[subst(a,r_b),subst(b,r_b)],[subst(r,r_b),subst(s,r_b)]]

M:=L*vector([x,t]) -- new coords

gamma:=1/sqrt(1-(v/c)^2)
normalize(M*(1/gamma))

-- end
)spool 