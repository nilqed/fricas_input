)abbrev domain CMAP CellMap
++
CellMap(R,n) : Exports == Implementation where

  R: Join(Ring,Comparable)
  n: NonNegativeInteger
  
  X   ==> Expression R
  DP  ==> DirectProduct
  OF  ==> OutputForm
  NNI ==> NonNegativeInteger
  MAP ==> List X -> List X
  DOM ==> List(Segment X)

  Exports == Join(CoercibleTo OutputForm) with
  
    _= : (%,%) -> Boolean
  
    cellMap   : (DOM,MAP) -> %
    getDom    : % -> DOM
    getMap    : % -> MAP
    faces     : % -> List List(%)
    coerce    : % -> OutputForm

  Implementation ==  add 
    
    Rep := Record(d:DOM,f:MAP)
    
    (x:% = y:%):Boolean == 
      l:NNI:=min(#(x.d),#(y.d))
      v:List X
      for j in 1..l repeat
        s:X:=subscript('z,[j::OF])::X
        v:=concat(v,s::X)
      x.d =y.d and (x.f) v = (y.f) v => true
      false
    
    cellMap(d:DOM,f:MAP):% == 
      #d > n => error concat("#DOM > ",string n)
      v:List X:=[1::X for j in 1..#d]
      ~test(#f(v)=n) => error concat("#Range ~= ", string n) 
      construct(d,f)

    faceLoHi(x:%,i:NNI,lo:Boolean):% ==
      l:NNI:=#(x.d)
      v:List X
      for j in 1..l repeat
        if j=i then
          if lo then
            s:X:=lo(x.d.i)
          else
            s:X:=hi(x.d.i)
        else
          if j>i then
            s:X:=subscript('%,[(j-1)::OF])::X
          else
            s:X:=subscript('%,[j::OF])::X
        v:=concat(v,s::X)
      vv:=delete(v,i..i)
      d:List(Segment X):=delete(x.d,i..i)
      f:MAP:=vv+->(x.f) v
      cellMap(d,f)
      

    faces(x:%):List List(%) ==
      l:NNI:=#(x.d)
      [[faceLoHi(x,j,true), faceLoHi(x,j,false)] for j in 1..l]
        

    getDom(x) == x.d
    getMap(x) == x.f
    
      
      
    coerce(x) == 
      v:List X
      for j in 1..#(x.d) repeat
        s:X:=subscript('%,[j::OF])::X
        v:=concat(v,s::X)
      r:List X:=(x.f) v
      hconcat ["|",x.d::OF," -> ",r::OF,"|"]
  
    
      
   
)abbrev domain CHCO ChainComplex
++
ChainComplex(R,n) : Exports == Implementation where

  NNI ==> NonNegativeInteger
  INT ==> Integer
  
  n : NNI
  R : Join(Ring,Comparable)
  
  CMAP ==> CellMap(R,n)
  CTOF ==> CoercibleTo OutputForm
  X    ==> Expression R
  OF   ==> OutputForm
  MAP  ==> List X -> List X
  DOM  ==> List(Segment X)

  Exports == Join(AbelianGroup ,CTOF, RetractableTo CMAP) with
 
    bdry      : % -> %
    size      : % -> NNI
    nthCoef   : (%,Integer) -> Integer
    nthFactor : (%,Integer) -> CMAP
    zero?     : % -> Boolean 
    _=        : (%,%) -> Boolean
    terms     : % -> List(Record(gen: CMAP,exp: Integer))
    mapGen    : ((CMAP -> CMAP),%) -> %
    mapCoef   : ((Integer -> Integer),%) -> %
    chainTerm : (DOM,MAP) -> %
    
    
    --coerce : % -> OutputForm
    
  Implementation == FreeAbelianGroup(CMAP) add

    Rep:=FreeAbelianGroup(CMAP)
      

    bdry(c:%):% == 
      if size(c) = 1 then
        s:=nthFactor(c,1)
        l:=faces(s)
        fs:=[(a.2::Rep-a.1::Rep) for a in l]
        sgn:=(j:INT):INT+->if even? (j-1) then 1 else -1
        nthCoef(c,1)*reduce("+",[sgn(j)*fs.j::Rep for j in 1..#fs])
      else
        ct:=[(nthCoef(c,j)*nthFactor(c,j))::Rep for j in 1..size(c)]
        reduce("+",map(bdry,ct))
        
 
    chainTerm(d:DOM,f:MAP):% == cellMap(d,f)$CMAP::%





     
